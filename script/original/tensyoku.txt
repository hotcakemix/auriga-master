-	script	ジョブチェンジャー	-1,{
	//getbaseclass( <class>,[<type>]);
	//入力値<class> : 職業値(Class)を入力
	//入力値<type> : 取得する前職業の種別を指定（省略可）
	//出力値 : 前職業のClass値

	// 使用するキャラ永続変数
	// upper_cnt : 転生回数を保持
	// OLD_CLASS : 転生前のクラスを保持

	//関数宣言
	function GetJobName;
	function Transmigration;
	function TransmigrationSub;
	function GetTransRequireBase;
	function GetTransRequireJob;
	function CheckTransLevel;
	function JobChangeTo3rd;

	// 設定
	if('config == 0){	// 起動後初めて呼ばれたときのみ実行
		// 転生回数上限
		set 'upper_limit,1;
		// 転生に必要なZeny
		set 'upper_zeny,300000;
		// 転生に必要なレベル
		set 'trans_require_base,99;		// 未転生⇒転生　ベース
		set 'trans_require_job,0;		// 未転生⇒転生　ジョブ
		set 'trans_require_base_upper,99;	// 転生２次⇒転生　ベース
		set 'trans_require_job_upper,0;	// 転生２次⇒転生　ジョブ
		set 'trans_require_base_3rd,99;	// ３次⇒転生　ベース
		set 'trans_require_job_3rd,0;		// ３次⇒転生　ジョブ

		// 転生ボーナス
		set 'status_bonus_rate,200;	// ステータスポイントに転生回数×'status_bonus_rateのボーナス
		set 'skill_bonus_rate,1;	// スキルポイントに転生回数×'skill_bonus_rateのボーナス

		// テコンのまま転生を許すか？
		set 'allow_trans_taekwon,0;	// 1で許す、それ以外で許さない
		// 養子の転生を許すか？
		// 養子から転生すると養子のノービスになります
		set 'allow_trans_baby,0;	// 1で許す、それ以外で許さない
		// 三次職を有効にする
		set 'enable_3rd_class,1;	// 1で有効、それ以外で無効

		if('enable_3rd_class == 1){
			// 三次職からの転生を許す
			// 三次職から転生するとハイノービスに戻ります。
			set 'allow_trans_3rd,1;		// 1で許す、それ以外で許さない
			// 養子から三次職への転職を許す
			set 'allow_babyto3rd,1;		// 1で許す、それ以外で許さない
			// 三次職転職時にステータスをリセットする
			set 'status_reset_3rd,0;	// 1でリセットする、それ以外でしない
			// 三次職転職時にスキルをリセットする
			set 'skill_reset_3rd,0;		// 1でリセットする、それ以外でしない

			// 三次職に転職するために必要なレベル
			set 'require_base_to3rd,99;	// ベース
			set 'require_job_to3rd,50;	// ジョブ
		}

		// 設定完了フラグ
		set 'config,1;
	}
	
	if(Job >= Job_RuneKnight) {
		if('allow_trans_3rd == 1 && (upper == 0 || upper == 1 || (upper == 2 && 'allow_trans_baby == 1))){
			Transmigration;
			close;
		} else {
			mes "[ジョブチェンジャー]";
			mes "あなたは3次職ですので";
			mes "これ以上の転職はできません。";
			close;
		}
	}
	
	if(Job < Job_Knight || Job == Job_Taekwon || Job == Job_Ninja || Job == Job_Gunslinger || Job == Job_SuperNovice) {	// ノービス or 一次 or テコン or 忍者 or スーパーノービス
		switch(upper){
		case 0:	//未転生
		case 2:	// 養子
			switch(job) {
			case Job_Novice:	// ノービス⇒一次職への転職
				if(JobLevel < 10) {
					//転職不可能、終了
					mes "[ジョブチェンジャー]";
					mes "転職条件に達していません。";
					mes "^0000FF「JobLv 10以上」";
					mes "一次職に転職するためには";
					mes "以上の条件を満たす必要があります。";
					close;
				}
				set upper_cnt,0;	//転生回数初期化
				mes "[ジョブチェンジャー]";
				mes "一次職への転職ですね。";
				mes "どの職業に転職しますか？";
				next;
				setarray '@jobname$,"^0000FF（剣）^000000ソードマン",
					"^0000FF（魔）^000000マジシャン","^0000FF（弓）^000000アーチャー",
					"^0000FF（聖）^000000アコライト","^0000FF（商）^000000マーチャント",
					"^0000FF（盗）^000000シーフ","^AA0000（初）^000000スーパーノービス",
					"^AA0000（闘）^000000テコンキッド","^AA0000（銃）^000000ガンスリンガー",
					"^AA0000（忍）^000000忍者","やめる";
				if(Upper == 2)		//養子
					cleararray '@jobname$[7],"",3;	//スパノビ以降は転職不可能
					set @menu,select(printarray('@jobname$));
				switch(@menu) {
				case 1: //剣士
					set '@changejob,Job_Swordman;
					break;
				case 2: //マジシャン
					set '@changejob,Job_Magician;
					break;
				case 3: //アーチャー
					set '@changejob,Job_Archer;
					break;
				case 4: //アコライト
					set '@changejob,Job_Acolyte;
					break;
				case 5: //商人
					set '@changejob,Job_Merchant;
					break;
				case 6: //シーフ
					set '@changejob,Job_Thief;
					break;
				case 7: //スーパーノービス
					if(BaseLevel < 45) {
						mes "[ジョブチェンジャー]";
						mes "スーパーノービスに転職するには";
						mes "ベースレベル45以上が必要です。";
						mes "またのご利用を、お待ちしております。";
						close;
					}
					set '@changejob,Job_SuperNovice;
					break;
				case 8: //テコンキッド
					set '@changejob,Job_TaeKwon;
					break;
				case 9: //ガンスリンガー
					set '@changejob,Job_Gunslinger;
					break;
				case 10://忍者
					set '@changejob,Job_Ninja;
					break;
				}
				GetJobName '@jobname$,'@changejob,0;
				mes "[ジョブチェンジャー]";
				mes "^0000FF" +'@jobname$+ "^000000に転職しますか？";
				next;
				if(select("転職する","やめる")==2) close;
				unequip;
				jobchange '@changejob,0;
				emotion 21;
				close;
			case Job_Swordman:
				setarray '@jobid,Job_Knight,Job_Crusader;
				break;
			case Job_Acolyte:
				setarray '@jobid,Job_Priest,Job_Monk;
				break;
			case Job_Magician:
				setarray '@jobid,Job_Wizard,Job_Sage;
				break;
			case Job_Merchant:
				setarray '@jobid,Job_Blacksmith,Job_Alchemist;
				break;
			case Job_Archer:
				if(Sex)
					setarray '@jobid,Job_Hunter,Job_Bard;
				else
					setarray '@jobid,Job_Hunter,Job_Dancer;
				break;
			case Job_Thief:
				setarray '@jobid,Job_Assassin,Job_Rogue;
				break;
			case Job_Taekwon:	//テコン
				setarray '@jobid,Job_StarGladiator,Job_SoulLinker;
				break;
			case Job_Ninja:		//忍者
				if(Sex)
					setarray '@jobid,Job_Kagerou;
				else
					setarray '@jobid,Job_Oboro;
				break;
			case Job_Gunslinger:  //
				setarray '@jobid,Job_Rebellion;
				break;			
			case Job_SuperNovice:	//スーパーノービス
				setarray '@jobid,Job_ExpandedSuperNovice;
				break;
			}
			if(JobLevel < 40) {
				//転職不可能、終了
				mes "[ジョブチェンジャー]";
				mes "転職条件に達していません。";
				mes "^0000FF「JobLv 40以上」";
				mes "二次職に転職するためには";
				mes "以上の条件を満たす必要があります。";
				break;
			}
			mes "[ジョブチェンジャー]";
			mes "二次職への転職ですね。";
			mes "どの職業に転職しますか？";
			next;
			//転職可能な職業を選択肢にセット
			GetJobName '@jobname$[0],'@jobid[0],0;
			if(Job != Job_Ninja && Job != Job_SuperNovice  && Job != Job_Gunslinger){
				GetJobName '@jobname$[1],'@jobid[1],0;
			}
			// テコンのまま転生
			if('allow_trans_taekwon == 1 && Job == Job_Taekwon && BaseLevel >= 99 && JobLevel >= 50){
				set '@jobname$[2],"転生する";
			}
				set '@jobname$[3],"やめる";
			switch(select(printarray('@jobname$))){
			case 1:	// 転職
			case 2:
				set '@changeto,@menu-1;
				break;
			case 3:	// 転生
				Transmigration;
				close;
			case 4:
				mes "[ジョブチェンジャー]";
				mes "またお越しください。";
				close; //やめる
			}
			mes "[ジョブチェンジャー]";
			mes "^0000FF" +'@jobname$['@changeto]+ "^000000に転職しますか？";
			next;
			if(select("転職する","やめる")==2){
				mes "[ジョブチェンジャー]";
				mes "またお越しください。";
				break; //やめる
			}
			unequip;//装備をはずす
			//キャラクターの転職
			jobchange '@jobid['@changeto],0;
			emotion 21;
			break;
		case 1:	// 転生済み
			if(job == 0){	// ハイノービス
				mes "[ジョブチェンジャー]";
				mes "あなたは^0000ffハイノービス^000000ですね。";
				next;
				if(JobLevel < 10){
					mes "[ジョブチェンジャー]";
					mes "あれ？JOBLvが足りないみたい。";
					mes "JOBLv10まであげてきてね。";
					break;
				}
				switch(OLD_CLASS){
				case CLASS_SNV: //スーパーノービス
					if(BaseLevel < 45) {
						mes "[ジョブチェンジャー]";
						mes "スーパーノービスに転職するには";
						mes "ベースレベル45以上が必要です。";
						mes "またのご利用を、お待ちしております。";
						close;
					}
					set '@tensei1,Job_SuperNovice;
					break;
				case CLASS_NJ:	set '@tensei1,Job_Ninja;break;
				case CLASS_SG2:
				case CLASS_SG:
				case CLASS_SL:	set '@tensei1,Job_Taekwon;break;
				case CLASS_GS:	set '@tensei1,Job_Gunslinger;break;
				case CLASS_CR:
				case CLASS_KN:	set '@tensei1,Job_Swordman;break;
				case CLASS_WZ:
				case CLASS_SA:	set '@tensei1,Job_Magician;break;
				case CLASS_DC:
				case CLASS_BA:
				case CLASS_HT:	set '@tensei1,Job_Archer;break;
				case CLASS_MO:
				case CLASS_PR:	set '@tensei1,Job_Acolyte;break;
				case CLASS_AM:
				case CLASS_BS:	set '@tensei1,Job_Merchant;break;
				case CLASS_RG:
				case CLASS_AS:	set '@tensei1,Job_Thief;break;
				case CLASS_KG:	set '@tensei1,Job_Ninja;break;
				case CLASS_OB:	set '@tensei1,Job_Ninja;break;
				case CLASS_RL:  set '@tensei1,Job_Gunslinger;break;
				}
				GetJobName '@jobname1$,'@tensei1,1;
				mes "[ジョブチェンジャー]";
				mes "^0000FF" +'@jobname1$+ "^000000に転職しますか？";
				next;
				if(select("転職する","やめる")==2) close;
				jobchange '@tensei1,1;
				emotion 21;
			} else if(job < 7 || job == 21 || job == 25 || job == 20){
				mes "[ジョブチェンジャー]";
				mes "転生2次職へ転職ですね。";
				next;
				if(JobLevel < 40){
					mes "[ジョブチェンジャー]";
					mes "あれ？JOBLvが足りないみたい。";
					mes "最低でも40は必要よ。";
					break;
				}
				switch(OLD_CLASS){
				case CLASS_SG2:
				case CLASS_SG:		set '@tensei2,Job_StarGladiator;break;
				case CLASS_SL:		set '@tensei2,Job_SoulLinker;break;
				case CLASS_KN2:
				case CLASS_KN:		set '@tensei2,Job_Knight;break;
				case CLASS_PR:		set '@tensei2,Job_Priest;break;
				case CLASS_WZ:		set '@tensei2,Job_Wizard;break;
				case CLASS_BS:		set '@tensei2,Job_Blacksmith;break;
				case CLASS_HT:		set '@tensei2,Job_Hunter;break;
				case CLASS_AS:		set '@tensei2,Job_Assassin;break;
				case CLASS_CR2:
				case CLASS_CR:		set '@tensei2,Job_Crusader;break;
				case CLASS_MO:		set '@tensei2,Job_Monk;break;
				case CLASS_SA:		set '@tensei2,Job_Sage;break;
				case CLASS_RG:		set '@tensei2,Job_Rogue;break;
				case CLASS_AM:		set '@tensei2,Job_Alchemist;break;
				case CLASS_BA:		set '@tensei2,Job_Bard;break;
				case CLASS_DC:		set '@tensei2,Job_Dancer;break;
				case CLASS_NJ:
					if(sex)
						set '@tensei2,Job_Kagerou;
					else
						set '@tensei2,Job_Oboro;
					break;
				case CLASS_SNV:	set '@tensei2,Job_ExpandedSuperNovice;break;
				}
				GetJobName '@jobname2$,'@tensei2,1;
				mes "[ジョブチェンジャー]";
				mes "^0000FF" +'@jobname2$+ "^000000に転職しますか？";
				next;
				if(select("転職する","やめる")==2) close;
				jobchange '@tensei2,1;
				emotion 21;
			}
			break;
		}
	}else{	// ２次職・テコン以外の特殊一次職
		switch(upper){
		case 0:	// 未転生
			Transmigration;
			close;
		case 1:	// 転生済み
			// 転生２次
			if(job >= Job_Knight && job <= Job_Dancer){
				if('enable_3rd_class == 1)	// 三次職有効
					JobChangeTo3rd;
				else				// 三次職無効
					Transmigration;
			// スパノビ、テコン系列、特殊一次職
			}else if(job > 19 && job < 26){
				Transmigration;
			}
			break;
		case 2:	// 養子
			if('enable_3rd_class == 1 && 'allow_babyto3rd == 1)
				JobChangeTo3rd;
			break;
		}
	}
	close;

	// 関数定義
	// JobChangeTo3rd
	// ３次に転職を行うサブルーチン
	function	JobChangeTo3rd {
		if(BaseLevel >= 'require_base_to3rd && JobLevel >= 'require_job_to3rd && job >= Job_Knight && job <= Job_Dancer){
			mes "[ジョブチェンジャー]";
			mes "3次職へ転職ですね。";
			next;
			switch(Class){
			case CLASS_KN:
			case CLASS_KN2:
			case CLASS_KN_H:
			case CLASS_KN_B:			
			case CLASS_KN2_H:
			case CLASS_KN2_B:	set '@tensei3,Job_RuneKnight;break;
			case CLASS_PR:
			case CLASS_PR_H:
			case CLASS_PR_B:	set '@tensei3,Job_ArchBishop;break;
			case CLASS_WZ:
			case CLASS_WZ_H:
			case CLASS_WZ_B:	set '@tensei3,Job_Warlock;break;
			case CLASS_BS:
			case CLASS_BS_H:
			case CLASS_BS_B:	set '@tensei3,Job_Mechanic;break;
			case CLASS_HT:
			case CLASS_HT_H:
			case CLASS_HT_B:	set '@tensei3,Job_Ranger;break;
			case CLASS_AS:
			case CLASS_AS_H:
			case CLASS_AS_B:	set '@tensei3,Job_Guillotine;break;
			case CLASS_CR:
			case CLASS_CR2:
			case CLASS_CR_H:
			case CLASS_CR_B:
			case CLASS_CR2_H:
			case CLASS_CR2_B:	set '@tensei3,Job_RoyalGuard;break;
			case CLASS_MO:
			case CLASS_MO_H:
			case CLASS_MO_B:	set '@tensei3,Job_Shura;break;
			case CLASS_SA:
			case CLASS_SA_H:
			case CLASS_SA_B:	set '@tensei3,Job_Sorcerer;break;
			case CLASS_RG:
			case CLASS_RG_H:
			case CLASS_RG_B:	set '@tensei3,Job_ShadowChaser;break;
			case CLASS_AM:
			case CLASS_AM_H:
			case CLASS_AM_B:	set '@tensei3,Job_Genetic;break;
			case CLASS_BA:
			case CLASS_BA_H:
			case CLASS_BA_B:	set '@tensei3,Job_Minstrel;break;
			case CLASS_DC:
			case CLASS_DC_H:
			case CLASS_DC_B:	set '@tensei3,Job_Wanderer;break;
			}
			GetJobName '@jobname3$,'@tensei3,0;
			mes "[ジョブチェンジャー]";
			mes "^0000FF" +'@jobname3$+ "^000000に転職しますか？";
			if('status_reset_3rd == 1){
				if('skill_reset_3rd == 1){
					mes "なお、^0000FF" + '@jobname3$ + "へ転職すると";
					mes "ステータスとスキルが全て";
					mes "リセットされます。";
				} else {
					mes "なお、^0000FF" + '@jobname3$ + "へ転職すると";
					mes "ステータスがリセットされます。";
				}
			} else if('skill_reset_3rd == 1){
				mes "なお、^0000FF" + '@jobname3$ + "へ転職すると";
				mes "スキルがリセットされます。";
			}
			if(select("転職する","やめる")==2) close;
			if('status_reset_3rd == 1) resetstatus;
			if('skill_reset_3rd == 1) resetskill;
			setoption 0x0000;
			jobchange '@tensei3,upper;
			//未熟なリンゴ配布
//			getitem 2795,1;
		
			//専用アイテム配布
//			switch('@tensei3){
//			case Job_RuneKnight:	//ルーンサークレット[1] 
//				set '@item,5746;
//				break;
//			case Job_Warlock: //魔力石の帽子[1]
//				set '@item,5753;
//				break;
//			case Job_Ranger:
//				set '@item,5748;//スナイパーゴーグル[1]
//				set '@item2,6124;//狼の笛
//				break;
//			case Job_ArchBishop:	//ミトラ[1]
//				set '@item,5747;
//				break;
//			case Job_Mechanic:	//ドライバーバンド[1] 
//				set '@item,5749;
//				break;
//			case Job_Guillotine:	//沈黙の執行者
//				set '@item,5755;
//				break;
//			case Job_RoyalGuard:	//シュミッツのヘルム[1]
//				set '@item,5757;
//				break;
//			case Job_Sorcerer:	//風のささやき[1]
//				set '@item,5756;
//				break;
//			case Job_Minstrel:	//ミンストレルソングの帽子[1] 
//				set '@item,5751;
//				break;
//			case Job_Wanderer:	//バレリーナの髪飾り[1] 
//				set '@item,5758;
//				break;
//			case Job_Shura:		//ブレイジングソウル[1] 
//				set '@item,5754;
//				break;
//			case Job_Genetic:	//ミダスのささやき[1]
//				set '@item,5752;
//				break;
//			case Job_ShadowChaser:
//				set '@item,5750; //シャドウクラウン[1]
//				set '@item2,6122;//ブラシ
//				set '@item3,6121;
//				break;
//			}
//			getitem '@item,1;
//			if('@tensei3 == Job_ShadowChaser || '@tensei3 == Job_Ranger) getitem '@item2,1;
//			if('@tensei3 == Job_ShadowChaser) getitem '@item3,1;
//			emotion 21;
		} else {
			mes "[ジョブチェンジャー]";
			mes "３次職に転職するためには";
			mes "ベースレベルが"+'require_base_to3rd+"";
			mes "ジョブレベルが"+'require_job_to3rd+"";
			mes "だけ必要になります。";
		}
		return;
	}
	// Transmigration
	// 転生処理を行うサブルーチン
	function	Transmigration {
		switch(TransmigrationSub()){
		case 0:	// 成功
			mes "[ジョブチェンジャー]";
			mes "転生完了しました。";
			mes "またのご利用を、お待ちしております。";
			break;
		case 1:	// 転生回数オーバー
			mes "[ジョブチェンジャー]";
			mes "あなたは限界まで転生を";
			mes "行いましたので、";
			mes "これ以上転生することはできません。";
			mes "何事も限度というものがありますよね…";
			break;
		case 2:	// レベル不足
			mes "[ジョブチェンジャー]";
			mes "転生条件に達していません。";
	 		break;
		case 3:	// カート・ファルコン・ペコペコ
			mes "[ジョブチェンジャー]";
			mes "カート、ファルコン、ペコペコ、";
			mes "ドラゴン、魔道ギア、ウォーグを";
			mes "外してから";
			mes "いらしてください。";
			break;
		case 4:	// ホムンクルス
			mes "[ジョブチェンジャー]";
			mes "ホムンクルスと別れてから";
			mes "いらしてください。";
			break;
		case 5:	// 費用不足
			mes "[ジョブチェンジャー]";
			mes "転生費用が足りませんよ";
			mes ""+'upper_zeny*upper_cnt+"z必要です。";
			break;
		}
		return;
	}
	function TransmigrationSub {
		if(upper_cnt >= 'upper_limit) return 1;
		mes "[ジョブチェンジャー]";
		mes "あなたが転生を行うためには";
		mes "次の条件を満たす必要があります。";
		next;
		mes "[ジョブチェンジャー]";
		mes "BaseLv ^0000FF"+GetTransRequireBase()+"^000000 以上";
		mes "JobLv ^0000FF"+GetTransRequireJob()+"^000000 以上";
		mes "^0000FF"+'upper_zeny*upper_cnt+"^000000 Zeny以上所持";
		mes " カート・ペコペコ・ファルコン";
		mes " ドラゴン・魔道ギア・ウォーグ";
		mes " を利用していない";
		next;
		mes "[ジョブチェンジャー]";
		mes "^0000FF必要Zenyは転生費用となり、";
		mes "消費されますので注意してください。^000000";
		mes "転生を行いますか？";
		next;
		if(select("転生する","やめる")==2) {
			close;
		//転生条件のチェック
		} else if(!CheckTransLevel()) return 2;			// レベル不足
	 	else if(checkcart() || checkfalcon() || checkriding() ||	// カート・ファルコン・ペコペコ
			checkdragon() || checkgear() || checkwolf()) return 3;	// ドラゴン・魔道ギア・ウォーグ
		else if(gethomuninfo(5)) return 4;					// ホムンクルス
		else if(Zeny < 'upper_zeny*upper_cnt) return 5;					// 費用不足
		set Zeny,Zeny-'upper_zeny*upper_cnt;
		set upper_cnt,upper_cnt+1;	// 転生回数加算
		set OLD_CLASS,getbaseclass(Class,2);		// 転生前クラスの保存
		
		jobchange 0,(upper == 2) ? 2 : 1;
		//ステータス・スキル・Lvのリセット
		set BaseLevel,1;
		resetstatus;
		resetskill;
		//ボーナスポイントを獲得
		set StatusPoint,'status_bonus_rate * upper_cnt;
		set SkillPoint,'skill_bonus_rate * upper_cnt;
		set PC_SHOP_POINT,PC_SHOP_POINT + 3000;
		emotion 46;
		return 0;	// 成功
	}
	// 転生可能なレベルの取得
	function GetTransRequireBase {
		if(Job >= Job_RuneKnight && Job <= Job_ShadowChaser)	// ３次
			return 'trans_require_base_3rd;
		switch(upper){
		case 0:	// 未転生
			return 'trans_require_base;
		case 1:	// 転生済み
			return 'trans_require_base_upper;
		}
		return 0;
	}
	function GetTransRequireJob {
		if(Job >= Job_Runeknight && Job <= Job_ShadowChaser)	// ３次
			return 'trans_require_job_3rd;
		switch(upper){
		case 0:	// 未転生
			return 'trans_require_job;
		case 1:	// 転生済み
			return 'trans_require_job_upper;
		}
		return 0;
	}
	// CheckTransLevel
	// 転生可能かどうかのレベル判定。可能なら1、不可能なら0を返す
	function CheckTransLevel {
		if(Job >= Job_Runeknight && Job <= Job_ShadowChaser)	// ３次
			return (BaseLevel >= 'trans_require_base_3rd && JobLevel >= 'trans_require_job_3rd) ? 1 : 0;
		switch(upper){
		case 0:	// 未転生
			return (BaseLevel >= 'trans_require_base && JobLevel >= 'trans_require_job) ? 1 : 0;
		case 1:	// 転生済み
			return (BaseLevel >= 'trans_require_base_upper && JobLevel >= 'trans_require_job_upper) ? 1 : 0;
		case 2:	// 養子（ここには到達しないはず）
			return 0;
		}
		return 0;	// 何のジョブかわからないためとりあえず不可
	}

	// GetJobName(<VAL>,<CLASS>,<Trans_Flag>) : CLASSに対応する職業名をVALにセット
	function GetJobName {
		switch(getarg(1)) {
		case Job_Novice:
			if(getarg(2)==1) set getarg(0),"ハイノービス";
			else set getarg(0),"ノービス";
			break;
		case Job_Swordman:
			if(getarg(2)==1) set getarg(0),"ハイソードマン";
			else set getarg(0),"ソードマン";
			break;
		case Job_Magician:
			if(getarg(2)==1) set getarg(0),"ハイマジシャン";
			else set getarg(0),"マジシャン";
			break;
		case Job_Archer:
			if(getarg(2)==1) set getarg(0),"ハイアーチャー";
			else set getarg(0),"アーチャー";
			break;
		case Job_Acolyte:
			if(getarg(2)==1) set getarg(0),"ハイアコライト";
			else set getarg(0),"アコライト";
			break;
		case Job_Merchant:
			if(getarg(2)==1) set getarg(0),"ハイマーチャント";
			else set getarg(0),"マーチャント";
			break;
		case Job_Thief:
			if(getarg(2)==1) set getarg(0),"ハイシーフ";
			else set getarg(0),"シーフ";
			break;
		case Job_Knight:
			if(getarg(2)==1) set getarg(0),"ロードナイト";
			else set getarg(0),"ナイト";
			break;
		case Job_Priest:
			if(getarg(2)==1) set getarg(0),"ハイプリースト";
			else set getarg(0),"プリースト";
			break;
		case Job_Wizard:
			if(getarg(2)==1) set getarg(0),"ハイウィザード";
			else set getarg(0),"ウィザード";
			break;
		case Job_Blacksmith:
			if(getarg(2)==1) set getarg(0),"ホワイトスミス";
			else set getarg(0),"ブラックスミス";
			break;
		case Job_Hunter:
			if(getarg(2)==1) set getarg(0),"スナイパー";
			else set getarg(0),"ハンター";
			break;
		case Job_Assassin:
			if(getarg(2)==1) set getarg(0),"アサシンクロス";
			else set getarg(0),"アサシン";
			break;
		case Job_Crusader:
			if(getarg(2)==1) set getarg(0),"パラディン";
			else set getarg(0),"クルセイダー";
			break;
		case Job_Monk:
			if(getarg(2)==1) set getarg(0),"チャンピオン";
			else set getarg(0),"モンク";
			break;
		case Job_Sage:
			if(getarg(2)==1) set getarg(0),"プロフェッサー";
			else set getarg(0),"セージ";
			break;
		case Job_Rogue:
			if(getarg(2)==1) set getarg(0),"チェイサー";
			else set getarg(0),"ローグ";
			break;
		case Job_Alchemist:
			if(getarg(2)==1) set getarg(0),"クリエイター";
			else set getarg(0),"アルケミスト";
			break;
		case Job_Bard:
			if(getarg(2)==1) set getarg(0),"クラウン";
			else set getarg(0),"バード";
			break;
		case Job_Dancer:
			if(getarg(2)==1) set getarg(0),"ジプシー";
			else set getarg(0),"ダンサー";
			break;
		case Job_SuperNovice:
			set getarg(0),"スーパーノービス";
			break;
		case Job_TaeKwon:
			set getarg(0),"テコンキッド";
			break;
		case Job_StarGladiator:
			set getarg(0),"拳聖";
			break;
		case Job_SoulLinker:
			set getarg(0),"ソウルリンカー";
			break;
		case Job_Gunslinger:
			set getarg(0),"ガンスリンガー";
			break;
		case Job_Ninja:
			set getarg(0),"忍者";
			break;
		case Job_RuneKnight:
			set getarg(0),"ルーンナイト";
			break;
		case Job_ArchBishop:
			set getarg(0),"アークビショップ";
			break;
		case Job_Warlock:
			set getarg(0),"ウォーロック";
			break;
		case Job_Mechanic:
			set getarg(0),"メカニック";
			break;
		case Job_Ranger:
			set getarg(0),"レンジャー";
			break;
		case Job_Guillotine:
			set getarg(0),"ギロチンクロス";
			break;
		case Job_RoyalGuard:
			set getarg(0),"ロイヤルガード";
			break;
		case Job_Shura:
			set getarg(0),"修羅";
			break;
		case Job_Sorcerer:
			set getarg(0),"ソーサラー";
			break;
		case Job_ShadowChaser:
			set getarg(0),"シャドウチェイサー";
			break;
		case Job_Genetic:
			set getarg(0),"ジェネティック";
			break;
		case Job_Minstrel:
			set getarg(0),"ミンストレル";
			break;
		case Job_Wanderer:
			set getarg(0),"ワンダラー";
			break;
		case Job_ExpandedSuperNovice:
			set getarg(0),"拡張スーパーノービス";
			break;
		case Job_Kagerou:
			set getarg(0),"影狼";
			break;
		case Job_Oboro:
			set getarg(0),"朧";
			break;
		case Job_Rebellion:
			set getarg(0),"リベリオン";
			break;
		}
		return;
	}
}

payon.gat,151,246,5	duplicate(ジョブチェンジャー)	ジョブチェンジャー	811
hu_fild03.gat,24,346,5	duplicate(ジョブチェンジャー)	ジョブチェンジャー	811
izlude.gat,124,121,5	duplicate(ジョブチェンジャー)	ジョブチェンジャー	811
prontera.gat,152,193,5	duplicate(ジョブチェンジャー)	ジョブチェンジャー	811

// 2012/11/3 こよりさんのスクリプトからの変更点
// ○影狼・朧・拡張スーパーノービスに対応。
